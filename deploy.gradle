apply plugin: 'java-library'
apply plugin: 'maven-publish'
apply plugin: 'signing'

group = 'dev.klepto.kweb3'
archivesBaseName = 'kweb3'
version = '1.0.0'

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'kweb3'
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'kweb3'
                description = 'Annotation-driven web3 contract interactions for Java'
                url = 'https://github.com/klepto/kweb3'
                licenses {
                    license {
                        name = 'The MIT License'
                        url = 'https://github.com/klepto/kweb3/blob/master/license'
                    }
                }
                developers {
                    developer {
                        id = 'klepto'
                        name = 'Augustinas R.'
                        email = 'klepto@inbox.lt'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/klepto/kweb3.git'
                    developerConnection = 'scm:git:https://github.com/klepto/kweb3.git'
                    url = 'https://github.com/klepto/kweb3'
                }
            }
        }
    }

    repositories {
        maven {
            url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            if (project.hasProperty('sonatypeUsername') && project.hasProperty('sonatypePassword')) {
                credentials {
                    username sonatypeUsername
                    password sonatypePassword
                }
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}